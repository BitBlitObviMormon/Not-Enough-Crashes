
dependencies {
	compileOnly project(path: ':notenoughcrashes')
}
def total_version = mod_version
setupMod(total_version, "api_version")
archivesBaseName = mod_id
version total_version

bintray {
	user = project.hasProperty('bintray_user') ? project.property('bintray_user') : ""
	key = project.hasProperty('bintray_api_key') ? project.property('bintray_api_key') : ""
	publications = ["MavenPublication"]
	publish = true //[Default: false] Whether version should be auto published after an upload
	pkg {
		repo = "libs"
		name = mod_id
		desc = mod_description
		websiteUrl = githubUrl
		issueTrackerUrl = "$githubUrl/issues"
		githubRepo = github_repo //Optional Github repository
		githubReleaseNotesFile = 'README.md' //Optional Github readme file
		licenses = [license]
		vcsUrl = githubUrl
		version {
			name = total_version
			released = new Date()
		}
	}

}


publishing {
	publications {
		MavenPublication(MavenPublication) {
			artifact(remapJar)

			artifact(sourcesJar) {
				classifier = "sources"
				builtBy remapSourcesJar
			}
			groupId maven_group
			artifactId mod_id
			version total_version
			pom.withXml {
				def root = asNode()
				root.appendNode('description', mod_description)
				root.appendNode('name', mod_id)
				root.appendNode('url', githubUrl)
				root.children().last() + pomConfig
			}
		}
	}
}